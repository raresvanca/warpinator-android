apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: 'kotlin-parcelize'
apply plugin: 'org.jetbrains.kotlin.plugin.serialization'
apply plugin: 'com.google.devtools.ksp'
apply plugin: 'com.google.dagger.hilt.android'

android {
    compileSdkVersion 36

    defaultConfig {
        applicationId 'slowscript.warpinator'
        minSdkVersion 23 // Required by compose
        targetSdkVersion 36
        versionCode 1090
        versionName "1.9"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose true
    }

    compileOptions {
        sourceCompatibility = 17
        targetCompatibility = 17
    }
    packagingOptions {
        resources {
            excludes += ['META-INF/INDEX.LIST', 'META-INF/io.netty.versions.properties']
        }
    }
    namespace 'slowscript.warpinator'
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        buildConfig true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Jetpack Compose
    def composeBom = platform('androidx.compose:compose-bom-alpha:2025.12.01')
    implementation composeBom

    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    implementation 'androidx.activity:activity-compose'

    // Material 3 Implementations
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material3.adaptive:adaptive'
    implementation 'androidx.compose.material3.adaptive:adaptive-layout'
    implementation 'androidx.compose.material3.adaptive:adaptive-navigation'
    implementation 'androidx.compose.material:material-icons-extended'

    // State management
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose'
    implementation 'androidx.lifecycle:lifecycle-service'
    implementation 'com.google.dagger:hilt-android:2.57.2'
    ksp 'com.google.dagger:hilt-compiler:2.57.2'
    implementation 'androidx.hilt:hilt-navigation-compose:1.3.0'

    implementation 'org.openjax.security:nacl:0.3.2' //Update available, but API is weird now
    implementation 'org.bouncycastle:bcpkix-jdk15to18:1.82'
    implementation 'io.grpc:grpc-netty:1.75.0'
    implementation 'io.grpc:grpc-okhttp:1.75.0'
    implementation('io.grpc:grpc-protobuf:1.75.0') {
        exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
    }
    implementation 'io.grpc:grpc-stub:1.75.0'
    implementation 'io.grpc:grpc-kotlin-stub:1.5.0'
    implementation("com.google.protobuf:protobuf-kotlin:3.25.8")
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'

    implementation 'com.github.tony19:logback-android:3.0.0'
    implementation 'androidx.preference:preference:1.2.1'
    implementation 'com.google.guava:guava:33.4.8-android'
    //This was included by gRPC anyway, so why not use it
    implementation 'org.jmdns:jmdns:3.5.8'
    //Device discovery worsened after update, let's see if this was the problem
    // Also, new versions require desugaring on Android 5 and 6
    implementation 'org.slf4j:slf4j-api:2.0.17' // For jmdns, it declares a too old dependency
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
    implementation 'com.google.zxing:core:3.5.3'

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0"

    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.25.8'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.75.0'
        }

        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:1.5.0:jdk8@jar"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
                kotlin {}
            }
            task.plugins {
                grpc {}
                grpckt {}
            }
        }
    }
}

//If there is a better way to get rid of Netty logging, let me know
configurations.configureEach {
    resolutionStrategy {
        dependencySubstitution {
            substitute module('ch.qos.logback:logback-classic') using module('com.github.tony19:logback-android:3.0.0')
        }
    }
}
